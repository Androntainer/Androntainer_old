buildscript {
    ext {
        android_version = '7.3.1'
        compose_version = '1.3.0'
        kotlin_version = '1.7.20'
    }
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.20.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.14'
    }
}

plugins {
    id 'com.android.application' version "$android_version" apply false
    id 'com.android.library' version "$android_version" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

subprojects {
    afterEvaluate {
        if (it.findProperty('publishLibrary') ?: false) {
            plugins.apply('com.vanniktech.maven.publish')
            version = api_version_name
            println("${it.displayName}: ${group}:${it.name}:${version}")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}